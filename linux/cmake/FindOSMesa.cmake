# Try to find OSMESA. Sets the following variables:
#   OSMESA_FOUND
#   OSMESA_INCLUDE_DIR
#   OSMESA_LIBRARIES

set( OSMESA_FOUND false )

set( OSMESA_INCLUDE_DIRS /opt/local/include/GL /usr/local/include/GL /usr/include/GL )
set( OSMESA_LIBRARY_DIRS /opt/local/lib /usr/local/lib /usr/lib )

set( OSMESA_LIB_SUFFIXES lib x86_64-linux-gnu arm-linux-gnueabihf aarch64-linux-gnu )

find_path( OSMESA_INCLUDE_DIR NAMES "osmesa.h" PATHS ${OSMESA_INCLUDE_DIRS} PATH_SUFFIXES include NO_DEFAULT_PATH )
find_path( OPENGL_INCLUDE_DIR NAMES "gl.h" PATHS ${OSMESA_INCLUDE_DIRS} PATH_SUFFIXES include NO_DEFAULT_PATH )
find_library( OSMESA_LIBRARY  NAMES "OSMesa" PATHS ${OSMESA_LIBRARY_DIRS} PATH_SUFFIXES ${OSMESA_LIB_SUFFIXES} NO_DEFAULT_PATH )
find_library( OPENGL_GLU_LIBRARY  NAMES "GLU" PATHS ${OSMESA_LIBRARY_DIRS} PATH_SUFFIXES ${OSMESA_LIB_SUFFIXES} NO_DEFAULT_PATH )

mark_as_advanced( OSMESA_INCLUDE_DIR OSMESA_LIBRARY )

if( OSMESA_INCLUDE_DIR AND EXISTS "${OSMESA_INCLUDE_DIR}/osmesa.h" )
    file( STRINGS "${OSMESA_INCLUDE_DIR}/osmesa.h" OSMESA_VERISON_MAJOR_LINE REGEX "^#define OSMESA_MAJOR_VERSION ([0-9]+).*$" )
    file( STRINGS "${OSMESA_INCLUDE_DIR}/osmesa.h" OSMESA_VERISON_MINOR_LINE REGEX "^#define OSMESA_MINOR_VERSION ([0-9]+).*$" )
    file( STRINGS "${OSMESA_INCLUDE_DIR}/osmesa.h" OSMESA_VERISON_PATCH_LINE REGEX "^#define OSMESA_PATCH_VERSION ([0-9]+).*$" )

    string( REGEX REPLACE "^#define OSMESA_MAJOR_VERSION ([0-9]+).*$" "\\1" OSMESA_VERSION_MAJOR "${OSMESA_VERISON_MAJOR_LINE}" )
    string( REGEX REPLACE "^#define OSMESA_MINOR_VERSION ([0-9]+).*$" "\\1" OSMESA_VERSION_MINOR "${OSMESA_VERISON_MINOR_LINE}" )
    string( REGEX REPLACE "^#define OSMESA_PATCH_VERSION ([0-9]+).*$" "\\1" OSMESA_VERSION_PATCH "${OSMESA_VERISON_PATCH_LINE}" )

    set( OSMESA_VERSION_STRING "${OSMESA_VERSION_MAJOR}.${OSMESA_VERSION_MINOR}.${OSMESA_VERSION_PATCH}" )

    set( OSMESA_MAJOR_VERSION "${OSMESA_VERSION_MAJOR}" )
    set( OSMESA_MINOR_VERSION "${OSMESA_VERSION_MINOR}" )
    set( OSMESA_PATCH_VERSION "${OSMESA_VERSION_PATCH}" )
endif()

include( FindPackageHandleStandardArgs )
find_package_handle_standard_args( 
	OSMesa 
	REQUIRED_VARS OSMESA_LIBRARY OSMESA_INCLUDE_DIR OPENGL_INCLUDE_DIR OPENGL_GLU_LIBRARY
	VERSION_VAR OSMESA_VERSION_STRING
)

SET( OPENGL_INCLUDE_DIR ${OSMESA_INCLUDE_DIR} )
SET( OPENGL_LIBRARIES ${OSMESA_LIBRARY} ${OPENGL_GLU_LIBRARY} )

if( OSMESA_FOUND )
	message( STATUS "  using OSMESA headers at: ${OSMESA_INCLUDE_DIR}" )
endif()
